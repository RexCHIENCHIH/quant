//@version=5
strategy(title="AV轉",shorttitle = "AV轉",
 overlay=false,pyramiding = 5)

//每次收K都清空委託單
strategy.cancel_all()

//未平倉的進場價
Entry_Price = strategy.opentrades.entry_price(strategy.opentrades - 1) 

//計算最近一次進場後，圖表上經歷了多少根K棒
Bars_Since_Entry = bar_index - 
 strategy.opentrades.entry_bar_index(strategy.opentrades - 1)

////////////////////////////////////////////////////////////////////////////

//指標計算
Length = input.int(5, minval=1, title="K_Length")
Reverse = input.float(2, minval=0.01, title="Reverse")

LowestBar = ta.lowestbars(low,Length)
HighestBar = ta.highestbars(high,Length)

var LongCounter = 0 //多方計數器，儲存可以進場的倒數K棒數
var ShortCounter = 0 //空方計數器，儲存可以進場的倒數K棒數

LongCounter := LongCounter > 0 ? LongCounter -1 : 0
ShortCounter := ShortCounter > 0 ? ShortCounter -1 : 0

var LL = 0.00 
var HH = 0.00 

//若 當下的K棒是一段時間內的最低點 且 一段時間內的最高點-K棒低點>一定的波動度
//破低 且 有波動，代表符合V轉的前置條件急跌
if LowestBar==0 and ta.highest(high,Length) - low > Reverse * 0.01 * close
    LongCounter := Length
    LL := low

if HighestBar==0 and high - ta.lowest(low,Length) > Reverse * 0.01 * close
    ShortCounter := Length
    HH := high

////////////////////////////////////////////////////////////////////////////

//進場
LongEntry =LongCounter > 0 and close > LL + ( Reverse * 0.01 * close )
ShortEntry =ShortCounter > 0 and close < HH - ( Reverse * 0.01 * close )

//多單進場
if LongEntry
    strategy.entry("LE", strategy.long) 
    LongCounter := 0

//空單進場
if ShortEntry
    strategy.entry("SE", strategy.short) 
    ShortCounter := 0

////////////////////////////////////////////////////////////////////////////

//背景顏色，多頭綠色，空頭紅色，空手灰色
colorBG = strategy.position_size > 0 ? color.new(color.green, 80) :
           strategy.position_size < 0 ? color.new(color.red, 80) :
           color.new(color.gray, 100)

//force_overlay：呈現在主圖
bgcolor(colorBG, force_overlay=true)

////////////////////////////////////////////////////////////////////////////

plot(LowestBar,"LowestBar",color.green)
plot(HighestBar,"HighestBar",color.red)
plot(LongCounter,"LongCounter",color.green)
plot(ShortCounter,"ShortCounter",color.red)
