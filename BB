//@version=5
strategy(title="布林通道",overlay=true)

//布林通道計算
Length = input.int(20, minval=1, maxval=200, title="長度", group="布林通道參數設定")
mult = input.float(2.0, minval=0.1, maxval=10, title="標準差倍數", group="布林通道參數設定")
Source = input.source(close,title="取樣價格")
type = input.string("逆勢突破", options = ["逆勢突破","順勢突破"])

//布林通道
basis = ta.ema(Source,Length)
dev = mult * ta.stdev(Source,Length)
upper = basis + dev
lower = basis - dev

//進場條件
var LongEntry = false
var ShortEntry = false

if type=="逆勢突破"
    LongEntry := ta.crossover(close,lower)
    ShortEntry := ta.crossunder(close,upper)
else if type=="順勢突破"
    LongEntry := ta.crossover(close,upper)
    ShortEntry := ta.crossunder(close,lower)

//多單進場
if LongEntry
    strategy.entry("LE", strategy.long) 

//空單進場
if ShortEntry
    strategy.entry("SE", strategy.short) 

//背景顏色，多頭綠色，空頭紅色，空手灰色
colorBG = strategy.position_size > 0 ? color.new(color.green, 80) :
           strategy.position_size < 0 ? color.new(color.red, 80) :
           color.new(color.gray, 100)

//force_overlay：呈現在主圖
bgcolor(colorBG, force_overlay=true)

//繪圖
plot(basis, "均線", color=color.gray)
plot(upper, "上通道", color.red)
plot(lower, "下通道", color.green)

