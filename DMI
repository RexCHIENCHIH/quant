
指標由三條線組成，分別是：
1.	正DI：表示多方的力量，值越高，漲勢越明顯。
2.	負DI：表示空方的力量，值越高，跌勢越明顯。
3.	ADX：趨勢強度指標，不考慮方向，只關注趨勢強度。當趨勢明顯時，ADX會上升。

//////////////////////////////////////////////////////////////////////////////////

//@version=5
strategy(title="DMI",overlay=false)

//DMI指標計算
DI_Length = input.int(14, minval=1, title="DI長度", group="DMI參數設定")
ADX_Smoothing = input.int(17, minval=1, title="ADX平滑度", group="DMI參數設定")
ADX_hine = input.int(30, minval=1, title="ADX閥值", group="DMI參數設定")
type = input.string("方向判斷", options = ["方向判斷","強度判斷"])

//趨向線(+DI、-DI)：n根k棒內，實際漲跌的動量百分比
//趨向值(DX)：計算+DI、-DI之間的差距，[(+DI)-(-DI)] / [(+DI)+(-DI)]=DX
//曲線平均線(ADX)：DX取平均，算出ADX，衡量趨勢強度
[diplus, diminus, adx] = ta.dmi(DI_Length, ADX_Smoothing)

//進場條件
var LongEntry = false
var ShortEntry = false

if type=="方向判斷"
    LongEntry := ta.crossover(diplus,diminus)
    ShortEntry := ta.crossunder(diplus,diminus)

else if type=="強度判斷"
    LongEntry := diplus>diminus and ta.crossover(adx,ADX_hine)
    ShortEntry := diplus<diminus and ta.crossover(adx,ADX_hine)

//多單進場
if LongEntry
    strategy.entry("LE", strategy.long) 

//空單進場
if ShortEntry
    strategy.entry("SE", strategy.short) 

//背景顏色，多頭綠色，空頭紅色，空手灰色
colorBG = strategy.position_size > 0 ? color.new(color.green, 80) :
           strategy.position_size < 0 ? color.new(color.red, 80) :
           color.new(color.gray, 100)

//force_overlay：呈現在主圖
bgcolor(colorBG, force_overlay=true)

//定義ADX線條顏色
adxColor = adx>=ADX_hine ? color.yellow : color.silver

//繪圖
plot(adx, color=adxColor, title="ADX")
plot(diplus, color=color.red, title="+DI")
plot(diminus, color=color.green, title="-DI")
hline(ADX_hine, "ADX閥值", color=color.new(color.gray, 50))
