//@version=5
strategy(title="MACD",overlay=false)

//每次收K都清空委託單
strategy.cancel_all()

//MACD指標計算
Fast_Lenght = input.int(12, minval=1, maxval=100, step=1, title="MACD快線", group="參數設定")
Slow_Length = input.int(26, minval=1, maxval=200, step=1, title="MACD慢線", group="參數設定")
Signal_Lenght = input.int(9, minval=1, maxval=100, step=1, title="信號平滑長度", group="參數設定")
Source = input.source(close,title="取樣價格")

//MACD 
fastMA = ta.ema(Source,Fast_Lenght) 
slowMA = ta.ema(Source,Slow_Length) 
DIF = fastMA - slowMA
MACD = ta.ema(DIF,Signal_Lenght) 
OSC = DIF - MACD

//進場條件
LongEntry =  ta.crossover(OSC,0)
ShortEntry =  ta.crossunder(OSC,0)

//出場條件
LongExit = math.min(OSC,OSC[1],OSC[2],OSC[3])>0 and OSC-OSC[3]>5
ShortExit = math.max(OSC,OSC[1],OSC[2],OSC[3])<0 and OSC-OSC[3]>5

//多單進場
if LongEntry
    strategy.entry("LE", strategy.long) 
if LongExit
    strategy.close("LE", "LX")

//空單進場
if ShortEntry
    strategy.entry("SE", strategy.short) 
if ShortExit
    strategy.close("SE", "SX")

//背景顏色，多頭綠色，空頭紅色，空手灰色
colorBG = strategy.position_size > 0 ? color.new(color.green, 80) :
           strategy.position_size < 0 ? color.new(color.red, 80) :
           color.new(color.gray, 100)

//force_overlay：呈現在主圖
bgcolor(colorBG, force_overlay=true)

//柱狀體顏色
var plotColor = color (na)
if OSC >= 0
    if OSC > OSC[1]
        plotColor := color.teal
    else
        plotColor := color.green

if OSC < 0
    if OSC > OSC[1]
        plotColor := color.fuchsia
    else
        plotColor := color.red

//繪製線圖
plot(OSC, title = "Histogram", style = plot.style_columns, color = plotColor)
plot(DIF, title = "快線",   color = color.blue)
plot(MACD, title = "慢線", color = color.orange)
