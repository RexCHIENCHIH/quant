//@version=5
strategy(title="RSI",overlay=false)

//參數設定
RSI_Length = input.int(14, minval=1, maxval=1000, step=2, title="RSI長度", group="RSI參數設定")
RSI_Upper = input.int(70, minval=0, maxval=100, step=5, title="RSI上軌", group="RSI參數設定")
RSI_Lower = input.int(30, minval=0, maxval=100, step=5, title="RSI下軌", group="RSI參數設定")
Source = input.source(close,title="取樣價格")
RSI = ta.rsi(Source,RSI_Length)
type = input.string("逆勢進場", options = ["逆勢進場","順勢進場"])

//進場條件
var LongEntry = false
var ShortEntry = false

if type=="逆勢進場"
    LongEntry := ta.crossover(RSI,RSI_Lower)
    ShortEntry := ta.crossunder(RSI,RSI_Upper)
else if type=="順勢進場"
    LongEntry := ta.crossover(RSI,RSI_Upper)
    ShortEntry := ta.crossunder(RSI,RSI_Lower)

//多單進場
if LongEntry
    strategy.entry("LE", strategy.long) 

//空單進場
if ShortEntry
    strategy.entry("SE", strategy.short) 

//RSI線條變色
rsiColor = RSI>=RSI_Upper ? color.red :
 RSI<=RSI_Lower ? color.green : color.blue

//繪圖
plot(RSI, "RSI", color=rsiColor, linewidth=2)
Upper = hline(RSI_Upper, "RSI上軌", color=color.gray)
Lower = hline(RSI_Lower, "RSI下軌", color=color.gray)
RSI_100 = hline(100, "RSI_100", color=color.gray,linestyle=hline.style_solid)
RSI_50 = hline(50, "RSI_50", color=color.new(color.gray, 50))
RSI_0 = hline(0, "RSI_0", color=color.gray,linestyle=hline.style_solid)

//填充
fill(Upper, RSI_100, color=color.new(color.red,90), title="超買區")
fill(RSI_0, Lower, color=color.new(color.green,90), title="超賣區")
