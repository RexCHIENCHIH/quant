//@version=5
strategy(title="標準差",overlay=false)

//標準差指標計算
Stdev_Length  = input.int(20, minval=1, title="長度", group="標準差參數設定")
Mult = input.float(1.5, minval=0.1, title="標準差倍數", group="標準差參數設定")
Big_Mult = input.float(3.0, minval=0.1, title="瞬間標準差倍數", group="標準差參數設定")
Source = input.source(close,title="取樣價格")
type = input.string("波動大", options = ["波動大","波動瞬間放大"])

//標準差
dev = ta.stdev(Source, Stdev_Length)
MA = ta.ema(Source, Stdev_Length)

//進場條件
var LongEntry = false
var ShortEntry = false

if type=="波動大"
    LongEntry := close>close[1]+dev*Mult and close>MA
    ShortEntry := close<close[1]-dev*Mult and close<MA
else if type=="波動瞬間放大"
    LongEntry := close>close[1]+dev*Big_Mult
    ShortEntry := close<close[1]-dev*Big_Mult

//多單進場
if LongEntry
    strategy.entry("LE", strategy.long) 

//空單進場
if ShortEntry
    strategy.entry("SE", strategy.short)

//背景顏色，多頭綠色，空頭紅色，空手灰色
colorBG = strategy.position_size > 0 ? color.new(color.green, 80) :
           strategy.position_size < 0 ? color.new(color.red, 80) :
           color.new(color.gray, 100)

//force_overlay：呈現在主圖
bgcolor(colorBG, force_overlay=true)

//繪圖
plot(dev, color=color.gray, title="標準差")
plot(MA, color=color.yellow,title="均線",force_overlay=true,linewidth = 2)

