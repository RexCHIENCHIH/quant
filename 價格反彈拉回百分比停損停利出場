
//@version=5
strategy(title="價格反彈拉回百分比 停損停利出場",overlay=true,max_bars_back=300)

//每次收K都清空委託單
strategy.cancel_all()

//未平倉的進場價
Entry_Price = strategy.opentrades.entry_price(strategy.opentrades - 1) 

//進場後經過了幾根K棒
Bars_SinceEntry = bar_index - 
 strategy.opentrades.entry_bar_index(strategy.opentrades - 1)

///均線參數
Small_Length = 20//input.int(20, "短均線的長度")
Big_Length = 50//input.int(50, "長均線的長度")
Source = input.source(close,"取樣價格")

//計算短均線與長均線
SmallMA = ta.sma(Source, Small_Length)
BigMA = ta.sma(Source, Big_Length)

// 設定拉回的條件
TP_Start = input.float(10.00,"獲利超過N%啟動") 
PB_Percent = input.float(50.00,"拉回%")       

//出場參數
SL = input.float(10.0, "停損百分比")  
TP = input.float(10.0, "停利百分比") 

//停利停損
Long_SL_Price = Entry_Price * (1 - SL * 0.01)
Long_TP_Price = Entry_Price * (1 + TP * 0.01)  
Short_SL_Price = Entry_Price * (1 + SL * 0.01)
Short_TP_Price = Entry_Price * (1 - TP * 0.01)  

//進場
LongCondition = ta.crossover(SmallMA, BigMA)

//計算進場後經歷過了幾根K棒
var Count_Bars_SinceEntry = 0

if strategy.position_size != strategy.position_size[1] and 
 strategy.position_size != 0
    Count_Bars_SinceEntry := 1

else if strategy.position_size != 0 and 
     strategy.position_size == strategy.position_size[1]
    Count_Bars_SinceEntry := Count_Bars_SinceEntry + 1

else if strategy.position_size == 0
    Count_Bars_SinceEntry := 0

HH = ta.highest(high,math.max(Count_Bars_SinceEntry,1))
LL = ta.lowest(low,math.max(Count_Bars_SinceEntry,1))

var High_SinceEntry = 0.00
var Low_SinceEntry = 0.00

if Count_Bars_SinceEntry > 0  

    High_SinceEntry := HH
    Low_SinceEntry := LL

else 
    High_SinceEntry := 0.00
    Low_SinceEntry := 0.00

//價格拉回、彈回條件啟動
var PB_Price = 0.00
var Over_Profit = false

if strategy.position_size > 0 and
     ( High_SinceEntry - Entry_Price ) / Entry_Price > TP_Start*0.01
    Over_Profit := true

else if strategy.position_size < 0 and
     ( Low_SinceEntry - Entry_Price ) / Entry_Price < -TP_Start*0.01
    Over_Profit := true

else if  strategy.position_size == 0
    Over_Profit := false    
    PB_Price := 0.00  

if strategy.position_size > 0 and Over_Profit == true
    PB_Price := High_SinceEntry - ( High_SinceEntry - Entry_Price )*PB_Percent*0.01

else if strategy.position_size < 0 and Over_Profit == true
    PB_Price := Low_SinceEntry + ( Entry_Price - Low_SinceEntry )*PB_Percent*0.01

else if Over_Profit == false

    if strategy.position_size > 0
        PB_Price := Long_SL_Price

    else if strategy.position_size < 0
        PB_Price := Short_SL_Price

//多單進場
if LongCondition and strategy.position_size<=0
    strategy.entry("LE", strategy.long)

//多單出場
if strategy.position_size > 0 
    strategy.exit("LX","LE", stop=PB_Price)

//背景顏色，多頭綠色，空頭紅色，空手灰色
colorBG = strategy.position_size > 0 ? color.new(color.green, 80) :
           strategy.position_size < 0 ? color.new(color.red, 80) :
           color.new(color.gray, 100)

//force_overlay：呈現在主圖
bgcolor(colorBG, force_overlay=true)

plot(SmallMA,"SmallMA",color.yellow)
plot(BigMA,"BigMA",color.blue)
plot(PB_Price[1]>0 ? PB_Price[1] : na,"PBPrice",color.red, style = plot.style_stepline)
