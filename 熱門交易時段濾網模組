
1. 只在特定時間進行交易，或希望避開某些熱門時段，這個濾網模組可以限制交易時間。


//////////////////////////////////////////////////////////////////////////////////

//@version=5
strategy(title="熱門交易時段濾網模組",overlay=true)

//每次收K都清空委託單
strategy.cancel_all()

//未平倉的進場價
Entry_Price = strategy.opentrades.entry_price(strategy.opentrades - 1) 

//參數設定
Small_Length = input.int(20, "短均長度")
Big_Length = input.int(50, "長均長度")
Source = input.source(close,title="取樣價格")

//出場參數
SL = input.float(5.0, "停損百分比")  
TP = input.float(5.0, "止盈百分比") 

//短均線、長均線
L1 = ta.sma(Source, Small_Length)
L2 = ta.sma(Source, Big_Length)

//多頭停利停損
Long_SL_Price = Entry_Price * (1 - SL * 0.01)  // 多頭止損價
Long_TP_Price = Entry_Price * (1 + TP * 0.01)  // TP1停利價

//濾網計算
TimeRange = input.session("0900-2300", "交易時間限制")

//t=當前圖表的時間框架，0900-2300的區間
t = time(timeframe.period, TimeRange)

var longFilter = false

//如果當前圖表的時間框架在0900-2300的區間，濾網打開，否則關閉
if time==t
    longFilter := true
else
    longFilter := false

//進場條件
longCondition1 = L1 > L2

//進場
if longCondition1 and longFilter
    strategy.entry("LE", strategy.long)

//出場
strategy.exit("Long_TP/SL","LE", limit=Long_TP_Price, stop=Long_SL_Price, comment_profit="TP", comment_loss="SL")

//均線繪圖
plot(L1,"SmallMA",color.yellow,force_overlay = true)
plot(L2,"BigMA",color.blue,force_overlay = true)

//若當前圖表的時間框架在0900-2300的區間，則背景上色
bgcolor(time == t ? color.new(color.orange,80) : na)

